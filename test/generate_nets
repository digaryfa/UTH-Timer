#!/bin/csh -f

# This script generates the dataset used to test the gate timing calculator of UTH-Timer (TimeKeepers' CCS timing calculation toolkit)

# Usage
#    generate_nets <testsuite_name>
#
# Example:
#    generate_nets tau2020_testsuite0130


# checking arguments
if ("$1" == "") then
  echo Usage:  $0 testsuite_name
  exit
endif

# Dataset name
set dataset = $1

# Number of circuits
set circuits_num = 2

# Enable pi models?
set pimodels = "--pimodels"

# Create Dataset (Circuits YAML, SPEF)

# Technology Library
set library = "../asap7/asap7sc7p5t_27/LIB/CCS/asap7sc7p5t_ALL_RVT_TT_ccs_201020.postprocessed.lib"

## Generate random dataset ##
# output will be $dataset.circuits.yaml and $dataset.spef
echo "../bin/gen_random_nets --liberty $library --dataset $dataset -n $circuits_num $pimodels --waveform asu_exp"
../bin/gen_random_nets --liberty $library --dataset $dataset -n $circuits_num $pimodels --waveform asu_exp

## Create SPICE decks ##
# output will be all the SPICE netlists in the $dataset.SPICE_decks folder

# simulator
set simulator = xyce

# delay calculation tool variables
# spice decks directory
set spice_decks = $dataset".spice_decks"

if ($simulator == "xyce") then
  # ASU 7nm library transistor models need preprocessing before being
  # used with Xyce (as instructed by TAU2020 contest committee)

  # spice lib transistor models
  set spice_models = "../asap7/asap7PDK_r1p7/models/xyce/7nm_TT.pm.modified"
  # spice lib gate models 
  set spice_lib = "../asap7/asap7sc7p5t_27/CDL/xAct3D_extracted/xyce/asap7sc7p5t_27_R_201024.sp.modified"
else if ($simulator == "spectre") then
  # spice lib transistor models
  set spice_models = "../asap7/asap7PDK_r1p7/models/spectre/7nm_TT.pm"
  # spice lib gate models 
  set spice_lib = "../asap7/asap7sc7p5t_27/CDL/xAct3D_extracted/spectre/asap7sc7p5t_27_R_201024.sp"
else
  echo "Simulator $simulator NOT Supported!"
endif

rm -rf $spice_decks
mkdir $spice_decks

echo "../bin/delay_calc_tool  --circuits $dataset.circuits.yaml   --liberty $library   --spef $dataset.spef   --spice_lib $spice_lib   --spice_models $spice_models   --gen_spice_decks   --spice_dir $spice_decks   --simulator $simulator "

../bin/delay_calc_tool \
  --circuits $dataset.circuits.yaml \
  --liberty $library \
  --spef $dataset.spef \
  --spice_lib $spice_lib \
  --spice_models $spice_models \
  --gen_spice_decks \
  --spice_dir $spice_decks \
  --simulator $simulator 
